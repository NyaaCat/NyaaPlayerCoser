plugins {
    id 'java-library'
}
// Version strings used for CI
// Note the spaces before and after the equals sign
ext.majorVersion = 0
ext.minorVersion = 1
ext.minecraftVersion = "1.13.2"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()

    maven {
        name 'Spigot'
        url 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }

    maven {
        name 'Sonatype'
        url 'https://oss.sonatype.org/content/groups/public'
    }

    maven {
        name 'NyaaCore'
        url 'https://raw.githubusercontent.com/NyaaCat/NyaaCentral/maven-repo'
    }

    maven {
        name 'ess-repo'
        url 'http://repo.ess3.net/content/groups/essentials'
    }

    maven {
        name 'sk89q'
        url 'http://maven.sk89q.com/artifactory/repo/'
    }

    maven {
        url "http://repo.dmulloy2.net/nexus/repository/public/"
    }

    maven {
        name 'vault-repo'
        url 'http://nexus.hc.to/content/repositories/pub_releases'
    }
}

dependencies {
    // spigot
    compileOnly 'org.spigotmc:spigot-api:1.13.2-R0.1-SNAPSHOT'
    compileOnly files('lib/spigot-1.13.2.jar')    // for NMS

    // other plugins
    if (gradle.hasProperty("useLocalDependencies") && gradle.useLocalDependencies) {
        compileOnly project(":NyaaCore")
        compileOnly project(":NyaaUtils")
        compileOnly project(":HamsterEcoHelper")
    } else {
        compileOnly('cat.nyaa:nyaacore:6.3-SNAPSHOT') { transitive = false }
        compileOnly('cat.nyaa:nyaautils:6.1-SNAPSHOT') { transitive = false }
        compileOnly('cat.nyaa:hamsterecohelper:6.2-SNAPSHOT') { transitive = false }
    }
    compileOnly group: "com.comphenix.protocol", name: "ProtocolLib", version: "4.4.0"

    // annotation processor
    compileOnly 'org.librazy:NyaaUtilsLangChecker:2.0-SNAPSHOT'
    annotationProcessor 'org.librazy:NyaaUtilsLangChecker:2.0-SNAPSHOT'
    annotationProcessor "org.spigotmc:spigot-api:$minecraftVersion-R0.1-SNAPSHOT"
}

// extra config for NyaaCore Language string
tasks.withType(JavaCompile) {
    String nyaaCoreLangDir = gradle.hasProperty("NyaaCoreLangDirs") ? gradle.NyaaCoreLangDirs : null
    if (nyaaCoreLangDir == null) { nyaaCoreLangDir = System.getenv("NYAACORE_LANG_DIR") }
    options.compilerArgs += ["-Xplugin:NyaaUtilsLangAnnotationProcessor", "-Xlint:deprecation"]
    if (nyaaCoreLangDir != null) {
        options.compilerArgs += ["-ALANG_DIR_ADDITIONAL_PATH=" + nyaaCoreLangDir]
    }
}

javadoc {
    (options as StandardJavadocDocletOptions).with {
        links 'https://docs.oracle.com/javase/8/docs/api/'
        links 'https://hub.spigotmc.org/javadocs/spigot/'
        links 'https://google.github.io/guava/releases/21.0/api/docs/'
        links 'https://nyaacat.github.io/NyaaCentral/NyaaCore/6.3-mc1.13.2/javadoc/'
        links 'https://nyaacat.github.io/NyaaCentral/HamsterEcoHelper/6.2-mc1.13.2/javadoc/'

        locale 'en_US'
        encoding 'UTF-8'
        docEncoding 'UTF-8'
        addBooleanOption('keywords', true)

        def currentJavaVersion = JavaVersion.current()
        if (currentJavaVersion > JavaVersion.VERSION_1_9) {
            options.addBooleanOption('html5', true)
        }

        windowTitle = "HamsterEcoHelper Javadoc"
        String fullVersion = System.getenv("FULL_VERSION_STRING")
        if (fullVersion != null) {
            docTitle = "<b>HamsterEcoHelper</b> " + fullVersion
        } else {
            docTitle = "<b>HamsterEcoHelper</b>"
        }
    }
}